whole_metrics = { soi ~ metrics ~ eoi }

metrics = { (
    host_metric
  | service_metric
  | role_metric
  | role_slot_metric
  | avg_metric
  | max_metric
  | min_metric
  | product_metric
  | diff_metric
  | divide_metric
  | scale_metric
  | offset_metric
  | percentile_metric
  | time_shift_metric
  | moving_average_metric
  | linear_regression_metric
  | group_metric
) }

metric_name_literal = ${ metric_name | "'" ~ metric_name ~ "'" | "\"" ~ metric_name ~ "\"" }
metric_name = @{ ( 'a'..'z' | 'A'..'Z' | '0'..'9' | "-" | "_" | "." | "*" )+ }

host_id_literal = ${ host_id | "'" ~ host_id ~ "'" | "\"" ~ host_id ~ "\"" }
host_id = @{ ( 'a'..'z' | 'A'..'Z' | '0'..'9' )+ }

host_metric = { "host" ~ "(" ~ host_id_literal ~ "," ~ metric_name_literal ~ ")" }

service_name_literal = ${ service_name | "'" ~ service_name ~ "'" | "\"" ~ service_name ~ "\"" }
service_name = @{ ( 'a'..'z' | 'A'..'Z' | '0'..'9' | "-" | "_" )+ }

service_metric = { "service" ~ "(" ~ service_name_literal ~ "," ~ metric_name_literal ~ ")" }

role_name = @{ ( 'a'..'z' | 'A'..'Z' | '0'..'9' | "-" | "_" )+ }
role_full_name_literal = ${ role_full_name | "'" ~ role_full_name ~ "'" | "\"" ~ role_full_name ~ "\"" }
role_full_name = { service_name ~ ":" ~ " "* ~ role_name }

role_metric = { "role" ~ "(" ~ role_full_name_literal ~ "," ~ metric_name_literal ~ ")" }

role_slot_metric = { "roleSlots" ~ "(" ~ role_full_name_literal ~ "," ~ metric_name_literal ~ ")" }

avg_metric = { "avg" ~ "(" ~ metrics ~ ")" }

max_metric = { "max" ~ "(" ~ metrics ~ ")" }

min_metric = { "min" ~ "(" ~ metrics ~ ")" }

product_metric = { "product" ~ "(" ~ metrics ~ ")" }

diff_metric = { "diff" ~ "(" ~ metrics ~ "," ~ metrics ~ ")" }

divide_metric = { "divide" ~ "(" ~ metrics ~ "," ~ metrics ~ ")" }

factor = { ( fraction | double ) }
double = @{ "-"? ~ ( '0'..'9'+ ~ ( "." ~ '0'..'9'* )? | '0'..'9'* ~ "." ~ '0'..'9'+ ) ~ ( ( "e" | "E" ) ~ ( "+" | "-" )? ~ '0'..'9'+ )? }
fraction = { double ~ "/" ~ double }

scale_metric = { "scale" ~ "(" ~ metrics ~ "," ~ factor ~ ")" }

offset_metric = { "offset" ~ "(" ~ metrics ~ "," ~ factor ~ ")" }

percentile_metric = { "percentile" ~ "(" ~ metrics ~ "," ~ double ~ ")" }

duration_literal = ${ duration | "'" ~ duration ~ "'" | "\"" ~ duration ~ "\"" }
duration = @{ '0'..'9'+ ~ ( "m" ~ "o"? | "h" | "d" | "w" | "y" ) }

time_shift_metric = { "timeShift" ~ "(" ~ metrics ~ "," ~ duration_literal ~ ")" }

moving_average_metric = { "movingAverage" ~ "(" ~ metrics ~ "," ~ duration_literal ~ ")" }

linear_regression_metric = { "linearRegression" ~ "(" ~ metrics ~ "," ~ duration_literal ~ ")" }

group_metric = { "group" ~ "(" ~ metrics ~ ( "," ~ metrics )* ~ ")" }

whitespace = _{ " " | "\t" | "\r" | "\n" }
